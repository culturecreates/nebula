PREFIX schema: <http://schema.org/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
SELECT 
?uri 
(sample(?name_language) as ?name) 
(sample(?parents) as ?parent)  
(sample(?addressLocality) as ?locality) 
(sample(?addressRegion) as ?region) 
(sample(?adid) as ?sameAs) 
(sample(?resultSeverity) as ?conforms)
WHERE {
    graph <GRAPH_PLACEHOLDER> {
       ?uri a ?type  . 
        OPTIONAL {
          ?uri schema:name ?name_language .
        }
        OPTIONAL {
          ?parent_entitys ?link  ?uri .
          filter(isblank(?uri))
          OPTIONAL {
            ?super_parent_entitys ?parent_link ?parent_entitys
            filter(isblank(?parent_entitys))
            filter(!isblank(?super_parent_entitys))
          }
          BIND (COALESCE(?super_parent_entitys, ?parent_entitys) as ?parents)
          filter(!isblank(?parents))
        }
        OPTIONAL {
            ?shacl sh:focusNode ?uri ;
                sh:resultSeverity ?resultSeverity .
        }
    }
    ?uri a schema:Place .      
    #filter(!isBLANK(?uri))  
    OPTIONAL {
      ?adid_sub schema:sameAs ?uri .
      filter(contains(str(?adid_sub),"http://kg.artsdata.ca/resource/K"))
    }

    OPTIONAL {
      ?uri schema:sameAs ?adid_obj .
      filter(contains(str(?adid_obj),"http://kg.artsdata.ca/resource/K"))
    }

    bind(COALESCE(?adid_obj, ?adid_sub) as ?adid)


    OPTIONAL {
      ?uri schema:address/schema:addressLocality  ?addressLocality .
    }

    OPTIONAL {
      ?uri schema:address/schema:addressRegion ?addressRegion  .
    }

} GROUP BY ?uri  
ORDER BY ?uri