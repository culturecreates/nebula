PREFIX schema: <http://schema.org/>
PREFIX adm: <http://kg.artsdata.ca/minted/>
PREFIX dbo: <http://dbpedia.org/ontology/>
select ?artsdata 
(sample(?names) as ?name) 
(sample(?wikidatas) as ?wikidata)  
(sample(?streetAddresses) as ?streetAddress)  
(sample(?localities) as ?locality) 
(sample(?regions) as ?region) 
(sample(?postalCodes) as ?postalCode)   
(sample(?timezones) as ?timezone)   
where {
   
    graph <http://kg.artsdata.ca/core> {
        ?artsdata a schema:Place ;
                  schema:name ?names .
        optional {
                ?artsdata  schema:address/schema:streetAddress  ?streetAddresses .}
        optional {
                ?artsdata  schema:address/schema:addressRegion  ?regions .}
        optional {
                ?artsdata  schema:address/schema:addressLocality  ?localities .}
        optional {
                ?artsdata schema:address/schema:postalCode  ?postalCodes .}
        optional {
                ?artsdata schema:address/schema:addressCountry ?country .}
        optional {
                ?artsdata  dbo:timeZone  ?timezones .}    

        filter(str(?country) = "CA")
    }
   
    optional {
        ?artsdata schema:sameAs ?wikidata_match .
        filter(strstarts(str(?wikidata_match),"http://www.wikidata.org/entity"))
    }
    bind(URI(?wikidata_match) as ?wikidatas)
}  group by ?artsdata 