PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX schema: <http://schema.org/>
PREFIX ex: <http://www.example.org#>
construct {
    ?wikidata schema:name ?wikidataLabel ;
               a schema:Organization ;
               a ex:WikidataEntity ;
              ex:SceneProMemberStatedInLabel ?statedLabel ;
              ex:SceneProMemberRetreivedDate ?date  ;
              schema:sameAs ?aduri .
    ?aduri a ex:ArtsdataEntity ; 
              schema:sameAs ?wikidata .    

} where {
    SERVICE <https://query.wikidata.org/sparql> {
        SELECT ?wikidata ?wikidataLabel ?statedLabel ?date ?aduri WHERE {
            ?wikidata wdt:P463 wd:Q87727957;
                      p:P463 ?statement .
            ?statement ps:P463  wd:Q87727957 .
            OPTIONAL {
               ?wikidata wdt:P7627 ?adid .
               bind(iri(concat("http://kg.artsdata.ca/resource/", ?adid)) as ?aduri)
            }
            OPTIONAL {
                ?statement prov:wasDerivedFrom ?ref.
                ?ref pr:P248 ?stated;
                     pr:P813 ?date.
            }
            SERVICE wikibase:label {
                bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en".
            }
        }
    }
}