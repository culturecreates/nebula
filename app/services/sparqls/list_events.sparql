PREFIX schema: <http://schema.org/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#>

SELECT ?uri (sample(?name_language) as ?name)  (sample(?location_name) as ?location) (sample(?additionalType) as ?type)  ?startDate ?endDate (sample(?sameas_action) as ?artsdata)  (sample(?resultSeverity) as ?conforms) 
WHERE {
    ?uri a schema:Event .
    graph <GRAPH_PLACEHOLDER> {
        ?uri schema:name  ?name_language ;
              a ?additionalType ;
             schema:startDate ?startDate . 
        OPTIONAL {  ?uri schema:endDate ?endDate .  }
        BIND(IF(datatype(?startDate) = xsd:date, xsd:dateTime(CONCAT(STR(?startDate), "T00:00:00")), ?startDate) AS ?normalizedStartDate)
        OPTIONAL {
          FILTER NOT EXISTS { ?series schema:subEvent ?uri . }
          
          #bind(concat("mint") as ?mint_top_level)
          bind(concat("<a class='btn btn-primary btn-sm' href='/mint/preview?externalUri=",ENCODE_FOR_URI(str(?uri)),"'>mint</a>") as ?mint_top_level)
          filter(?normalizedStartDate > now())
          filter(NOT EXISTS {  ?shacl sh:focusNode ?uri })
        }
        OPTIONAL {
            ?shacl sh:focusNode ?uri ;
                sh:resultSeverity ?resultSeverity .
        }
         OPTIONAL {
          ?uri a ?additionalType .
        }
    } 
    OPTIONAL {
      ?adid_sub schema:sameAs ?uri .
      filter(contains(str(?adid_sub),"http://kg.artsdata.ca/resource/K"))
    }

    OPTIONAL {
      ?uri schema:sameAs ?adid_obj .
      filter(contains(str(?adid_obj),"http://kg.artsdata.ca/resource/K"))
    }

    bind(COALESCE(?adid_obj, ?adid_sub) as ?adid)

    OPTIONAL {
        ?uri  schema:location/schema:name ?location_name .
    }

    BIND(COALESCE(?adid, ?mint_top_level) as ?sameas_action)
   
    

} GROUP BY ?uri  ?normalizedStartDate ?startDate   ?endDate 
ORDER BY DESC(?normalizedStartDate)